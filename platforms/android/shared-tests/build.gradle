import groovy.json.JsonSlurper

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 29

    def versionFile = file('../../../lerna.json')
    def parsedVersion = new JsonSlurper().parseText(versionFile.text)
    project.version = parsedVersion.version.toString()

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled false
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs += ['../../../packages/test-www/dist', '../../../packages/nimbus-bridge/dist/iife']
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    androidTestImplementation project(":annotations")
    androidTestImplementation "com.salesforce.k2v8:k2v8:$k2v8_version"
    androidTestImplementation project(":bridge-v8")
    androidTestImplementation project(":bridge-webview")
    androidTestImplementation "com.eclipsesource.j2v8:j2v8:$j2v8_version"
    androidTestImplementation "com.google.guava:guava:29.0-android"
    kaptAndroidTest project(":compiler-webview")
    kaptAndroidTest project(":compiler-v8")

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.google.truth:truth:$google_truth_version"
    androidTestImplementation "io.kotlintest:kotlintest-runner-junit4:$kotlin_test_runner_version"
}

/*
 * Compile the test web app prior to assembling the androidTest app
 */
task buildTestWebApp(type: Exec) {
    workingDir '../../..'
    commandLine 'npm', 'install'
}

tasks.whenTaskAdded { task ->
    if (task.name ==~ "assemble.*AndroidTest") {
        task.dependsOn buildTestWebApp
    }
}

apply from: rootProject.file('gradle/lint.gradle')
