import groovy.json.JsonSlurper

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 29

    def versionFile = file('../../../lerna.json')
    def parsedVersion = new JsonSlurper().parseText(versionFile.text)
    project.version = parsedVersion.version.toString()

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled false
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    api project(":annotations")
    api project(":core")
    api project(":k2v8")
    kapt project(":compiler-v8")

    implementation "com.eclipsesource.j2v8:j2v8:$j2v8_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.google.truth:truth:$google_truth_version"
    androidTestImplementation "io.kotlintest:kotlintest-runner-junit4:$kotlin_test_runner_version"
    kaptAndroidTest project(":compiler-v8")

    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.google.truth:truth:$google_truth_version"
    kaptTest project(":compiler-v8")
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

apply from: rootProject.file('gradle/lint.gradle')
apply from: rootProject.file('gradle/publishing.gradle')
