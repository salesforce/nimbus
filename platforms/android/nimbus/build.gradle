apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['../../../test-www/dist']
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    api project(":nimbus-annotations")
    kapt project(":nimbus-compiler")

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-nop:1.7.25'
    testImplementation 'org.json:json:20180130'
    testImplementation 'io.kotlintest:kotlintest-runner-junit4:3.1.5'
    testImplementation "io.mockk:mockk:1.9.3.kotlin12"
    kaptTest project(":nimbus-compiler")

    androidTestImplementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

/*
 * Compile the test web app prior to assembling the androidTest app
 */
task buildTestWebApp(type: Exec) {
    workingDir '../../../test-www'
    commandLine 'npm', 'run', 'build'
}

tasks.whenTaskAdded { task ->
    if (task.name ==~ "assemble.*AndroidTest") {
        task.dependsOn buildTestWebApp
    }
}
