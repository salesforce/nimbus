import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath(Libs.androidToolsBuildGradle)
        classpath(Libs.kotlinGradlePlugin)
        classpath(Libs.gradleBintrayPlugin)
        classpath(Libs.dokkaGradlePlugin)
        classpath(Libs.kotlinSerialization)
        classpath(Libs.buildInfoExtractorGradle)
    }
}

plugins{
    id("com.jfrog.artifactory")
    id("maven-publish")
    id("com.vanniktech.android.junit.jacoco") version "0.16.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://dl.bintray.com/salesforce-mobile/android"
        }
    }
    group GROUP
    def versionFile = file("$rootDir/../../lerna.json")
    def parsedVersion = new JsonSlurper().parseText(versionFile.text)
    version = parsedVersion.version.toString()

    junitJacoco {
        jacocoVersion = Versions.jacoco // type String
        ignoreProjects = [] // type String array
        excludes // type String List
        includeNoLocationClasses = false // type boolean
        includeInstrumentationCoverageInMergedReport = false // type boolean
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_API_KEY')
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}
